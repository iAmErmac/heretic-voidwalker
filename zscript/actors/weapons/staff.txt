
Class HVRStaff : HereticVRWeapon replaces Staff
{
	Default
	{
	Weapon.SelectionOrder 3801;
	+THRUGHOST
	+WEAPON.WIMPY_WEAPON
	+WEAPON.MELEEWEAPON
	+WEAPON.NOALERT
	//+WEAPON.OFFHANDWEAPON
	Weapon.SisterWeapon "HVRStaffPowered";
	Obituary "$OB_MPSTAFF";
	Tag "$TAG_STAFF";
	}
	
	action void A_StaffAttackX (int damage, class<Actor> puff)
	{
		FTranslatedLineTarget t;

		if (player == null)
		{
			return;
		}

		int laflags = LAF_ISMELEEATTACK;
		int alflags = 0;
		Weapon weapon = invoker == player.OffhandWeapon ? player.OffhandWeapon : player.ReadyWeapon;
		if (weapon != null)
		{
			laflags |= weapon.bOffhandWeapon ? LAF_ISOFFHAND : 0;
			alflags |= weapon.bOffhandWeapon ? ALF_ISOFFHAND : 0;
			if (!weapon.DepleteAmmo (weapon.bAltFire))
				return;
		}
		double ang = angle + Random2[StaffAtk]() * (5.625 / 256);
		double slope = AimLineAttack (ang, DEFMELEERANGE, flags: alflags);
		LineAttack (ang, DEFMELEERANGE, slope, damage, 'Melee', puff, laflags, t);
	}

	States
	{
	Ready:
		TNT1 A 0;
		TNT1 A 0 A_JumpIf(invoker.owner.player.mo.countinv("HVRRuneShield") == 1, "ReadyShield"); 
		STFF A 1 A_WeaponReady;
		Loop;
	ReadyShield:
		STFF S 1 Bright A_WeaponReady;
		Goto Ready;
	Deselect:
		STFF A 0 A_JumpIf(invoker.fastSwitchWeapon == true, 2);
		STFF A 1 A_Lower;
		Loop;
		STFF A 1 A_Lower (120);
		Loop;
	Select:
		STFF A 0 A_JumpIf(invoker.fastSwitchWeapon == true, 2);
		STFF A 1 A_Raise;
		Loop;
		STFF A 1 A_Raise (120);
		Loop;
	Fire:
		#### # 0 A_JumpIf(invoker.owner.player.mo.countinv("HVRRuneShield") == 1, "DontFire"); 
		STFF BCDEFG 1;
		STFF H 1 A_StaffAttackX(random[StaffAttack](5, 20), "StaffPuff");
		STFF FDC 1;
		STFF A 4;
		STFF A 8 A_ReFire;
		Goto Ready;
	DontFire:
		#### # 10;
		Goto Ready;
	AltFire:
		#### # 20 {
			if(invoker.owner.player.mo.countinv("HVRRuneShield"))
				invoker.owner.player.mo.takeinventory("HVRRuneShield", 1);
			else
				invoker.owner.player.mo.giveinventory("HVRRuneShield", 1);
		}
		Goto Ready;
	}
}

Class HVRStaffPowered : HVRStaff
{
	Default
	{
	Weapon.SisterWeapon "HVRStaff";
	Weapon.ReadySound "weapons/staffcrackle";
	+WEAPON.POWERED_UP
	+WEAPON.READYSNDHALF
	+WEAPON.STAFF2_KICKBACK
	Obituary "$OB_MPPSTAFF";
	Tag "$TAG_STAFFP";
	}

	States
	{
	Ready:
		TNT1 A 0;
		STFF A 1 A_WeaponReady;
		Loop;
	Deselect:
		STFF A 1 A_Lower;
		Loop;
	Select:
		STFF A 1 A_Raise;
		Loop;
	Fire:
		STFF BCDEFG 1;
		STFF H 1 A_StaffAttackX(random[StaffAttack](18, 81), "StaffPuff2");
		STFF FDC 1;
		STFF A 4;
		STFF A 8 A_ReFire;
		Goto Ready;
	}
}

Class HVRRuneShield : Inventory
{
	Default
	{
	Inventory.maxamount  1;
	}
}