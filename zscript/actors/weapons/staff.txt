
Class HVRStaff : HereticVRWeapon replaces Staff
{
	Default
	{
	Weapon.SelectionOrder 3801;
	+THRUGHOST
	+WEAPON.WIMPY_WEAPON
	+WEAPON.MELEEWEAPON
	+WEAPON.NOALERT
	//+WEAPON.OFFHANDWEAPON
	Weapon.SisterWeapon "HVRStaffPowered";
	Obituary "$OB_MPSTAFF";
	Tag "$TAG_STAFF";
	}
	
	action void A_StaffAttackX (int damage, class<Actor> puff)
	{
		FTranslatedLineTarget t;

		if (player == null)
		{
			return;
		}

		int laflags = LAF_ISMELEEATTACK;
		int alflags = 0;
		Weapon weapon = invoker == player.OffhandWeapon ? player.OffhandWeapon : player.ReadyWeapon;
		if (weapon != null)
		{
			laflags |= weapon.bOffhandWeapon ? LAF_ISOFFHAND : 0;
			alflags |= weapon.bOffhandWeapon ? ALF_ISOFFHAND : 0;
			if (!weapon.DepleteAmmo (weapon.bAltFire))
				return;
		}
		double ang = angle + Random2[StaffAtk]() * (5.625 / 256);
		double slope = AimLineAttack (ang, DEFMELEERANGE, flags: alflags);
		LineAttack (ang, DEFMELEERANGE, slope, damage, 'Melee', puff, laflags, t);
	}

	States
	{
	Ready:
		TNT1 A 0;
		STFF A 0 A_SetExtraModel("NHST");
		#### # 0 A_JumpIf(invoker.owner.player.mo.countinv("HVRRuneShield") == 1, "ReadyShield"); 
		#### # 1 A_WeaponReady;
		Loop;
	ReadyShield:
		#### S 1 Bright A_WeaponReady;
		Goto Ready;
	Deselect:
		STFF A 0 A_SetExtraModel("NHST");
		#### # 0 A_JumpIf(invoker.fastSwitchWeapon == true, 2);
		#### # 1 A_Lower;
		Loop;
		#### # 1 A_Lower (120);
		Loop;
	Select:
		STFF A 0 A_SetExtraModel("NHST");
		#### # 0 A_JumpIf(invoker.fastSwitchWeapon == true, 2);
		#### # 1 A_Raise;
		Loop;
		#### # 1 A_Raise (120);
		Loop;
	Fire:
		STFF A 0 A_SetExtraModel("NHST");
		#### # 0 A_JumpIf(invoker.owner.player.mo.countinv("HVRRuneShield") == 1, "DontFire"); 
		#### BCDEFG 1;
		#### H 1 A_StaffAttackX(random[StaffAttack](5, 20), "StaffPuff");
		#### FDC 1;
		#### A 4;
		#### # 8 A_ReFire;
		Goto Ready;
	DontFire:
		#### # 10;
		Goto Ready;
	AltFire:
		#### # 20 {
			if(invoker.owner.player.mo.countinv("HVRRuneShield"))
				invoker.owner.player.mo.takeinventory("HVRRuneShield", 1);
			else
				invoker.owner.player.mo.giveinventory("HVRRuneShield", 1);
		}
		Goto Ready;
	Placeholder:
		NHST ABCDEFGH 1;
		Stop;
	}
}

Class HVRStaffPowered : HVRStaff
{
	Default
	{
	Weapon.SisterWeapon "HVRStaff";
	Weapon.ReadySound "weapons/staffcrackle";
	+WEAPON.POWERED_UP
	+WEAPON.READYSNDHALF
	+WEAPON.STAFF2_KICKBACK
	Obituary "$OB_MPPSTAFF";
	Tag "$TAG_STAFFP";
	}

	States
	{
	Fire:
		STFF A 0 A_SetExtraModel("NHST");
		#### # 0 A_JumpIf(invoker.owner.player.mo.countinv("HVRRuneShield") == 1, "DontFire"); 
		#### BCDEFG 1;
		#### H 1 A_StaffAttackX(random[StaffAttack](18, 81), "StaffPuff2");
		#### FDC 1;
		#### A 4;
		#### # 8 A_ReFire;
		Goto Ready;
	}
}

Class HVRRuneShield : Inventory
{
	Default
	{
	Inventory.maxamount  1;
	}
}