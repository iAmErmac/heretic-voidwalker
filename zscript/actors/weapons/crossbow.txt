
Class HVRCrossbow : HereticVRWeapon replaces Crossbow
{
	Default
	{
	Weapon.SelectionOrder 800;
	Weapon.AmmoUse 1;
	Weapon.AmmoGive 10;
	Weapon.AmmoType "CrossbowAmmo";
	Weapon.SisterWeapon "HVRCrossbowPowered";
	Inventory.PickupMessage "$TXT_WPNCROSSBOW";
	Tag "$TAG_CROSSBOW";
	}
	
	action void A_FireCrossbowX ()
	{
		if (player == null)
		{
			return;
		}
		int alflags = 0;
		Weapon weapon = invoker == player.OffhandWeapon ? player.OffhandWeapon : player.ReadyWeapon;
		if (weapon != null)
		{
			alflags |= weapon.bOffhandWeapon ? ALF_ISOFFHAND : 0;
			if (!weapon.DepleteAmmo (weapon.bAltFire))
				return;
		}
		
		if(invoker.showParticles)
		{
			SpawnPlayerMissile ("EtheralBolt1", aimflags: alflags);
			SpawnPlayerMissile ("EtheralBolt3", angle - 4.5, aimflags: alflags);
			SpawnPlayerMissile ("EtheralBolt3", angle + 4.5, aimflags: alflags);
		}
		else
		{
			SpawnPlayerMissile ("CrossbowFX1", aimflags: alflags);
			SpawnPlayerMissile ("CrossbowFX3", angle - 4.5, aimflags: alflags);
			SpawnPlayerMissile ("CrossbowFX3", angle + 4.5, aimflags: alflags);
		}
	}

	States
	{
	Spawn:
		TNT1 A 0 A_JumpIf(invoker.pickupModel == 1, "SpawnVoxel");
		TNT1 A 0 A_JumpIf(invoker.pickupModel == 2, "SpawnModel");
		WBOW A 1;
		Loop;
	SpawnVoxel:
		BOWV A 1;
		Goto Spawn;
	SpawnModel:
		BOWM A 1;
		Goto Spawn;
	Ready:
		TNT1 A 0;
		CRBW A 0 A_SetExtraModel("NHBW");
		#### # 1 A_WeaponReady;
		Loop;
	Deselect:
		CRBW A 0 A_SetExtraModel("NHBW");
		#### # 0 A_JumpIf(invoker.fastSwitchWeapon == true, 2);
		#### # 1 A_Lower;
		Loop;
		#### # 1 A_Lower (120);
		Loop;
	Select:
		CRBW A 0 A_SetExtraModel("NHBW");
		#### # 0 A_JumpIf(invoker.fastSwitchWeapon == true, 2);
		#### # 1 A_Raise;
		Loop;
		#### # 1 A_Raise (120);
		Loop;
	Fire:
		CRBW A 0 A_SetExtraModel("NHBW");
		#### BE 1;
		#### # 0 A_FireCrossbowX;
		#### # 0 A_GunFlash("Flash");
		#### FGHIJKLMN 2;
		#### A 6;
		#### # 5 A_ReFire;
		Goto Ready;
	Flash:
		TNT1 A 0 A_jumpIf(invoker.showFLashSprite == false, "NoFlash");
		CRFL ABCD 2 Bright;
		Stop;
	NoFlash:
		TNT1 A 1;
		Stop;
	Placeholder:
		NHBW ABCDFGHIJKLMN 1;
	}
}

Class HVRCrossbowPowered : HVRCrossbow
{
	Default
	{
	+WEAPON.POWERED_UP
	Weapon.AmmoGive 0;
	Weapon.SisterWeapon "HVRCrossbow";
	Tag "$TAG_CROSSBOWP";
	}
	
	action void A_FireCrossbowX2()
	{
		if (player == null)
		{
			return;
		}

		int alflags = 0;
		Weapon weapon = invoker == player.OffhandWeapon ? player.OffhandWeapon : player.ReadyWeapon;
		if (weapon != null)
		{
			alflags |= weapon.bOffhandWeapon ? ALF_ISOFFHAND : 0;
			if (!weapon.DepleteAmmo (weapon.bAltFire))
				return;
		}
		
		if(invoker.showParticles)
		{
			SpawnPlayerMissile ("EtheralBolt2", aimflags: alflags);
			SpawnPlayerMissile ("EtheralBolt2", angle - 4.5, aimflags: alflags);
			SpawnPlayerMissile ("EtheralBolt2", angle + 4.5, aimflags: alflags);
			SpawnPlayerMissile ("EtheralBolt4", angle - 9., aimflags: alflags);
			SpawnPlayerMissile ("EtheralBolt4", angle + 9., aimflags: alflags);
		}
		else
		{
			SpawnPlayerMissile ("CrossbowFX2", aimflags: alflags);
			SpawnPlayerMissile ("CrossbowFX2", angle - 4.5, aimflags: alflags);
			SpawnPlayerMissile ("CrossbowFX2", angle + 4.5, aimflags: alflags);
			SpawnPlayerMissile ("CrossbowFX3", angle - 9., aimflags: alflags);
			SpawnPlayerMissile ("CrossbowFX3", angle + 9., aimflags: alflags);
		}
	}

	States
	{
	Fire:
		CRBW A 0 A_SetExtraModel("NHBW");
		#### BE 1;
		#### # 0 A_FireCrossbowX2;
		#### # 0 A_GunFlash("Flash");
		#### FGHIJKLMN 2;
		#### A 1;
		#### # 4 A_ReFire;
		Goto Ready;
	}
}