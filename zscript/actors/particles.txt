
mixin Class particleConfig
{
	protected bool reduceParticles;
	protected hvr_Settings _settings;
	
	override void PostBeginPlay()
	{
		PlayerInfo player = players[consoleplayer];
		if(!player) return;
		
		_settings = hvr_Settings.of();
		reduceParticles = _settings.lessParticles();
	}
}

Class RocketTrailParticle : Actor
{
	Default
	{
	scale 0.4;
	radius 1;
	height 1;
	Gravity 0;
	damage 0;
	Renderstyle "Add";
	Alpha 0.95;
	BounceFactor 0.3;
	+MISSILE
	+CLIENTSIDEONLY
	+NOTELEPORT
	+NOBLOCKMAP
	+CORPSE
	+BLOODLESSIMPACT 
	+FORCEXYBILLBOARD
	+NODAMAGETHRUST
	+MOVEWITHSECTOR
	+NOBLOCKMONST
	-SOLID
	+THRUACTORS
	+DONTSPLASH
	-NOGRAVITY
	+DONTBLAST
	}
	
	States
	{
	Spawn:
		TNT1 A 3;
		SPRK AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 BRIGHT A_SetScale(scalex*0.96);
		Stop;
	}
}

Class HVRStaffPuff : Actor
{
	Default
	{
	VSpeed 0;
	Renderstyle "Add";
	Alpha 0.9;
	Scale 0.13;
	Damagetype "Staff";
	+FORCEXYBILLBOARD
	-ALLOWPARTICLES
	+NOEXTREMEDEATH
	+PUFFONACTORS
	+ALWAYSPUFF
	+MTHRUSPECIES
	+NOEXTREMEDEATH
	-ALWAYSPUFF
	+THRUGHOST
	}
	
	States
	{
	Melee:
	Spawn:
		TNT1 AAAAA 0 A_SpawnItemEx("StaffSpark",0,0,4,frandom(-3.2,3.2),frandom(-3.2,3.2),frandom(-3.2,3.2));
		KTPF A 1 Bright Light("STAFPUFF01") A_PlaySound("STAFHIT1");
	Death:
		KTPF A 0 A_SetScale(scalex*0.7,scaley*0.7);
		KTPF A 1 Bright Light("STAFPUFF02")A_FadeOut(0.1);
		KTPF A 0 A_SetScale(scalex*0.7,scaley*0.7);
		KTPF A 1 Bright Light("STAFPUFF03")A_FadeOut(0.1);
		KTPF A 0 A_SetScale(scalex*0.7,scaley*0.7);
		KTPF A 1 Bright Light("STAFPUFF04")A_FadeOut(0.1);
		KTPF A 0 A_SetScale(scalex*0.7,scaley*0.7);
		KTPF A 1 Bright Light("STAFPUFF05")A_FadeOut(0.1);
	Fade:
		KTPF A 0 A_SetScale(scalex*0.7,scaley*0.7);
		KTPF A 1 Bright A_FadeOut(0.1);
		Loop;
	XDeath:
		TNT1 A 1 A_PlaySound("STAFHIT2");
		Stop;
	}
}

Class StaffSpark : Actor
{
	Default
	{
	Radius 2;
	Height 2;
	RenderStyle "Add";
	Alpha 0.9;
	Scale 0.045;
	+NOINTERACTION
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+NOGRAVITY
	+DONTSPLASH
	}
  
	States
	{
	Spawn:
		KTPF A 1 Bright A_FadeOut(0.05);
		KTPF A 0 Bright A_SetScale(scalex*0.79,scaley*0.79);
		KTPF A 0 A_ChangeVelocity(frandom(-1,1),frandom(-1,1),frandom(-1,1),CVF_RELATIVE);
		loop;
	}
}

Class HVRPoweredStaffPuff : Actor
{
	Default
	{
	+NOBLOCKMAP
	+NOGRAVITY
	+PUFFONACTORS
	+FORCEXYBILLBOARD
	RenderStyle "Add";
	Alpha 0.9;
	VSpeed 0.0;
	Scale 0.34;
	Decal "WallCrack";
	DamageType "PowerStaff";
	}
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_PlaySound("STAFHIT1");
		Goto Puff;
	XDeath:
		TNT1 A 0 A_PlaySound("STAFPNCH");
	Puff:
		TNT1 AAAAAAAA 0 A_CustomMissile ("BlueExplosionParticle2", 3, 0, random (0, 360), 2, random (0, 360));
		PUF4 AABBCCDDEEFF 1 Bright A_SpawnItemEx("BlueStaffPuffFlare",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
		Stop;
	}
}

Class BlueExplosionParticle2 : RocketTrailParticle
{
	Default
	{
	scale 0.04;
	Gravity 0.17;
	Speed 7;
	+DOOMBOUNCE
	}
	
	States
	{
	Spawn:
		SPKB AAAAAAAAAAAAAAAAAA 1 BRIGHT A_SetScale(scalex*0.92);
		Stop;
	}
}

Class FairyDustTrail : MageWandSmoke
{
	mixin particleConfig;
	
	Default
	{
	Renderstyle "Add";
	Alpha 1;
	Scale 0.7;
	+DONTSPLASH
	}
	
	States
	{
	Spawn:
		FXFD ABCD 3 Bright A_FadeOut(0.1);
		FXFD ABCD 2 A_Fadeout(0.2);
		TNT1 A 0 A_Remove(0);
		Stop;
	Bounce:
		TNT1 A 0 ThrustThingZ(0,25,0,0);
		FXFD ABCD 2;
		TNT1 A 0 A_Remove(0);
		Stop;
	}
}

Class GoldWandTracer : FastProjectile
{
	mixin particleConfig;
	
	Default
	{
	Radius 2;
	Height 2;
	Speed 200;
	ProjectileKickback 1;
	DamageType "Goldwand";
	Decal "PlasmaScorchLower";
	Projectile;
	Renderstyle "Add";
	Alpha 0.90;
	Scale 1;
	DeathSound "GWANDHIT";
	MissileType "GoldWandAfterImage";
	MissileHeight 8;
	+RANDOMIZE
	+FORCEXYBILLBOARD
	+BLOODSPLATTER
	+SPECTRAL
	+CANNOTPUSH
	}
	
	States
	{
	Spawn:
		FX01 CD 3 Bright;
		Loop;
	Death:
		TNT1 A 0 A_JumpIf(invoker.reduceParticles, "DeathAlt");
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("GoldWandParticle",0,0,0,frandom(-6,6),frandom(-6,6),frandom(-6,6),0,SXF_NOCHECKPOSITION, 32);
		TNT1 AA 1 Light("GOLDWANDSHOT2");
		TNT1 AA 1 Light("GOLDWANDSHOT3");
		TNT1 AA 1 Light("GOLDWANDSHOT4");
		TNT1 AA 1 Light("GOLDWANDSHOT5");
		Stop;
	DeathAlt:
		TNT1 A 0 A_SpawnItemEx("GoldWandSparks");
		TNT1 AA 1 Light("GOLDWANDSHOT2");
		TNT1 AA 1 Light("GOLDWANDSHOT3");
		TNT1 AA 1 Light("GOLDWANDSHOT4");
		TNT1 AA 1 Light("GOLDWANDSHOT5");
		Stop;
	}
}

Class GoldWandTracer2 : GoldWandTracer
{
	Default
	{
	MissileType "GoldWandAfterImage2";
	}
}

Class GoldWandAfterImage : FairyDustTrail
{
	Default
	{
	Renderstyle "Add";
	Alpha 1.0;
	Scale 0.3;
	+NOGRAVITY
	+NOINTERACTION
	}
	
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 0 A_JumpIf(invoker.reduceParticles, "SpawnReduced");
		FX01 CCDD 1 
		{
			A_FadeOut(0.25);
			A_SpawnItemEX("FairyDustYellowLite",0,0,0,frandom(0.6,-0.6),frandom(0.6,-0.6),frandom(0.6,-0.6), 0, 0, 128);
			A_SpawnItemEX("FairyDustYellowLite",0,0,0,frandom(0.6,-0.6),frandom(0.6,-0.6),frandom(0.6,-0.6), 0, 0, 196);
		}
		Stop;
	SpawnReduced:
		TNT1 A 1 { if(random(0,10) > 2) A_SpawnParticle("f8b800", SPF_FULLBRIGHT, 24, random(2, 3), 0, 0,0,0, frandom(0.2,-0.2),frandom(0.2,-0.2),frandom(0.2,-0.2), 0,0,0, 0.9); }
		Stop;
	}
}

Class GoldWandAfterImage2 : GoldWandAfterImage
{
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 0 A_JumpIf(invoker.reduceParticles, "SpawnReduced");
		FX01 CCDD 1 
		{
			A_FadeOut(0.25);
			A_SpawnItemEX("FairyDustYellowLite",0,0,0,frandom(0.6,-0.6),frandom(0.6,-0.6),frandom(0.6,-0.6), 0, SXF_NOCHECKPOSITION, 196);
		}
		Stop;
	SpawnReduced:
		TNT1 A 1 { if(random(0,10) > 4) A_SpawnParticle("f8b800", SPF_FULLBRIGHT, 35, random(2, 3), 0, 0,0,0, frandom(0.2,-0.2),frandom(0.2,-0.2),frandom(0.2,-0.2), 0,0,0, 0.9); }
		Stop;
	}
}

Class GoldWandFX3 : GoldWandFX2
{
	mixin particleConfig;
	
	Default
	{
	Radius 2;
	Height 2;
	Decal "PlasmaScorchLower";
	}
	
	States
	{
	Spawn:
		FX01 CCCDDD 1 Bright A_SpawnItemEX("GoldWandSlowAfterImage");
		Loop;
	Death:
		TNT1 A 0 A_JumpIf(invoker.reduceParticles, "DeathAlt");
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("GoldWandParticle",0,0,0,frandom(-6,6),frandom(-6,6),frandom(-6,6),0,SXF_NOCHECKPOSITION, 32);
		FX01 EEE 1 Light("GOLDWANDSHOT2");
		FX01 FFF 1 Light("GOLDWANDSHOT3");
		FX01 GGG 1 Light("GOLDWANDSHOT4");
		FX01 HHH 1 Light("GOLDWANDSHOT5");
		Stop;
	DeathAlt:
		TNT1 A 0 A_SpawnItemEx("GoldWandSparks");
		FX01 EEE 1 Light("GOLDWANDSHOT2");
		FX01 FFF 1 Light("GOLDWANDSHOT3");
		FX01 GGG 1 Light("GOLDWANDSHOT4");
		FX01 HHH 1 Light("GOLDWANDSHOT5");
		Stop;
	}
}

Class GoldWandSlowAfterImage : FairyDustTrail
{
	Default
	{
	Renderstyle "Add";
	Alpha 1.0;
	Scale 0.3;
	+NOGRAVITY
	+NOINTERACTION
	}
	
	States
	{
	Spawn:
		FX01 CCDD 1 
		{
			A_FadeOut(0.25);
			if(invoker.reduceParticles)
			{
				if(random(0,10) > 7) A_SpawnParticle("f8b800", SPF_FULLBRIGHT, 24, random(2, 4), 0, 0,0,0, frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(-2.0,2.0), 0,0,0, 0.9);
			}
			else
			{
				A_SpawnItemEX("FairyDustYellowLite",0,0,0,frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(-2.0,2.0),0,SXF_NOCHECKPOSITION, 128);
				A_SpawnItemEX("FairyDustYellowLite",0,0,0,frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(-2.0,2.0),0,SXF_NOCHECKPOSITION, 220);
			}
		}
		Stop;
	}
}

Class FairyDustYellow : FairyDustTrail
{
	Default
	{
	Scale 0.3;
	Gravity 0;
	}
	
	States
	{
	Spawn:
		FXFD EFGH 3 Bright A_FadeOut(0.1);
		FXFD EFGH 2 A_Fadeout(0.2);
		TNT1 A 0 A_Remove(0);
		Stop;
	}
}

Class FairyDustYellowLite : FairyDustYellow
{
	Default
	{
	RenderStyle "Translucent";
	}
}

Class GoldWandParticle : Actor
{
	Default
	{
	Scale 0.085;
	Gravity 0.02;
	Radius 1;
	Height 1;
	Damage 0;
	Renderstyle "Add";
	Alpha 0.95;
	Speed 0;
	+DOOMBOUNCE
	+MISSILE
	+CLIENTSIDEONLY
	+NOTELEPORT
	+NOBLOCKMAP
	+CORPSE
	+BLOODLESSIMPACT 
	+FORCEXYBILLBOARD
	+NODAMAGETHRUST
	+MOVEWITHSECTOR
	+NOBLOCKMONST
	-SOLID
	+THRUACTORS
	+DONTSPLASH
	-NOGRAVITY
	+DONTBLAST
	}
	
	States
	{
	Spawn:
		SPRK AAAAAAAAA 1 Bright A_SetScale(scalex*0.86);
		Stop;
	}
}

Class GoldWandSparks : Actor
{
	Default
	{
	Radius 1;
	Height 1;
	Speed 0;
	+NOINTERACTION
	}
	
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 AAAAAA 0 {
			if(random(0,10) > 3) A_SpawnParticle("FFFF96", SPF_FULLBRIGHT, 10, random(2, 4), 0, 0,0,0, frandom(-6,6),frandom(-6,6),frandom(-6,6), 0,0,0, 1.0, 0.05);
		}
		TNT1 A 1;
		Stop;
	}
}